import aQute.bnd.osgi.Domain

import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

task writeProjectTemplateVersions

File mavenWrapperPropertiesFile = file("src/main/resources/maven-wrapper/.mvn/wrapper/maven-wrapper.properties")

Properties mavenWrapperProperties = GUtil.loadProperties(mavenWrapperPropertiesFile)

String mavenDistributionVersion = mavenWrapperProperties["distributionUrl"]

mavenDistributionVersion = mavenDistributionVersion.find(/\d+\.\d+\.\d+/)

Set<Project> projectTemplateProjects = project.parent.subprojects.findAll {
	it.name.startsWith "project-templates-"
}

buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.2.0"
	}

	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
	}
}

bundle {
	includeTransitiveDependencies = true
}

configurations {
	mavenDistribution
	projectTemplates
	projectTemplatesTests

	testCompile.extendsFrom(projectTemplatesTests)
}

dependencies {
	compileInclude group: "com.beust", name: "jcommander", version: "1.72"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: mavenDistributionVersion

	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.activator", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.api", version: "1.0.4"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.content.targeting.report", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.content.targeting.rule", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.content.targeting.tracking.action", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.control.menu.entry", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.form.field", version: "1.0.1"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.fragment", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.freemarker.portlet", version: "1.0.0"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.layout.template", version: "1.0.1"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.mvc.portlet", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.panel.app", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.portlet", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.portlet.configuration.icon", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.portlet.provider", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.portlet.toolbar.contributor", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.rest", version: "1.0.2"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.service", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.service.builder", version: "1.0.4"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.service.wrapper", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.simulation.panel.entry", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.soy.portlet", version: "1.0.0"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.spring.mvc.portlet", version: "1.0.0"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.template.context.contributor", version: "1.0.3"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.theme", version: "1.0.4"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.theme.contributor", version: "1.0.2"
	projectTemplates group: "com.liferay", name: "com.liferay.project.templates.workspace", version: "1.0.4"
	projectTemplates project(":sdk:project-templates-npm-angular-portlet")
	projectTemplates project(":sdk:project-templates-npm-billboardjs-portlet")
	projectTemplates project(":sdk:project-templates-npm-isomorphic-portlet")
	projectTemplates project(":sdk:project-templates-npm-jquery-portlet")
	projectTemplates project(":sdk:project-templates-npm-metaljs-portlet")
	projectTemplates project(":sdk:project-templates-npm-portlet")
	projectTemplates project(":sdk:project-templates-npm-react-portlet")
	projectTemplates project(":sdk:project-templates-npm-vuejs-portlet")
	projectTemplates project(":sdk:project-templates-war-hook")
	projectTemplates project(":sdk:project-templates-war-mvc-portlet")

	projectTemplatesTests project(":sdk:project-templates-activator")
	projectTemplatesTests project(":sdk:project-templates-api")
	projectTemplatesTests project(":sdk:project-templates-content-targeting-report")
	projectTemplatesTests project(":sdk:project-templates-content-targeting-rule")
	projectTemplatesTests project(":sdk:project-templates-content-targeting-tracking-action")
	projectTemplatesTests project(":sdk:project-templates-control-menu-entry")
	projectTemplatesTests project(":sdk:project-templates-form-field")
	projectTemplatesTests project(":sdk:project-templates-fragment")
	projectTemplatesTests project(":sdk:project-templates-freemarker-portlet")
	projectTemplatesTests project(":sdk:project-templates-layout-template")
	projectTemplatesTests project(":sdk:project-templates-mvc-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-angular-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-billboardjs-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-isomorphic-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-jquery-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-metaljs-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-react-portlet")
	projectTemplatesTests project(":sdk:project-templates-npm-vuejs-portlet")
	projectTemplatesTests project(":sdk:project-templates-panel-app")
	projectTemplatesTests project(":sdk:project-templates-portlet")
	projectTemplatesTests project(":sdk:project-templates-portlet-configuration-icon")
	projectTemplatesTests project(":sdk:project-templates-portlet-provider")
	projectTemplatesTests project(":sdk:project-templates-portlet-toolbar-contributor")
	projectTemplatesTests project(":sdk:project-templates-rest")
	projectTemplatesTests project(":sdk:project-templates-service")
	projectTemplatesTests project(":sdk:project-templates-service-builder")
	projectTemplatesTests project(":sdk:project-templates-service-wrapper")
	projectTemplatesTests project(":sdk:project-templates-simulation-panel-entry")
	projectTemplatesTests project(":sdk:project-templates-soy-portlet")
	projectTemplatesTests project(":sdk:project-templates-spring-mvc-portlet")
	projectTemplatesTests project(":sdk:project-templates-template-context-contributor")
	projectTemplatesTests project(":sdk:project-templates-theme")
	projectTemplatesTests project(":sdk:project-templates-theme-contributor")
	projectTemplatesTests project(":sdk:project-templates-war-hook")
	projectTemplatesTests project(":sdk:project-templates-war-mvc-portlet")
	projectTemplatesTests project(":sdk:project-templates-workspace")

	provided group: "org.apache.maven.archetype", name: "archetype-common", version: "2.4"

	testCompile gradleTestKit()
	testCompile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.2.0"
	testCompile group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "net.diibadaaba.zipdiff", name: "zipdiff", version: "1.0"
	testCompile project(":test:maven-executor")
}

liferayOSGi {
	expandCompileInclude = true
}

processResources {
	from configurations.projectTemplates
}

processTestResources {
	from configurations.projectTemplatesTests
}

test {
	dependsOn projectTemplateProjects.collect {
		"${it.path}:install"
	}

	dependsOn writeProjectTemplateVersions

	doFirst {
		Properties gradleWrapperProperties = new Properties()

		File gradleWrapperPropertiesFile = file("src/main/resources/gradle-wrapper/gradle/wrapper/gradle-wrapper.properties")

		gradleWrapperPropertiesFile.withInputStream {
			gradleWrapperProperties.load it
		}

		String gradleDistributionUrl = gradleWrapperProperties["distributionUrl"]

		File gradleDistributionFile = FileUtil.get(project, gradleDistributionUrl)

		systemProperty "gradle.distribution", uri(gradleDistributionFile)
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	jvmArgs "-XX:MaxMetaspaceSize=256m"
	maxHeapSize = "1024m"

	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
	systemProperty "test.debug.bundle.diffs", System.properties["test.debug.bundle.diffs"]

	systemProperties projectTemplateTokens
}

writeProjectTemplateVersions {
	doLast {
		Properties properties = new Properties()

		for (File projectTemplateTestFile in configurations.projectTemplatesTests) {
			Domain domain = Domain.domain(projectTemplateTestFile)
			String bundleSymbolicName = domain.bundleSymbolicName.key
			String bundleVersion = domain.bundleVersion
			properties.setProperty bundleSymbolicName, bundleVersion
		}

		File projectTemplateVersionsFile = new File(buildDir, "project-template-versions.properties")

		projectTemplateVersionsFile.parentFile.mkdirs()

		projectTemplateVersionsFile.withOutputStream {
			properties.store it, null
		}
	}
}